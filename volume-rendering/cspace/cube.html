<!DOCTYPE html>
<html>
<head>
<script id="vertex-shader" type="x-shader/x-vertex">
attribute vec4 vPosition;

uniform mat4 model2clip;
uniform mat4 model2world;

varying vec4 fPosition;
varying vec3 worldSpaceCoords;

void main()
{
    gl_Position = model2clip * vPosition;
    fPosition = model2clip * vPosition;
    fPosition = fPosition / fPosition.w;
    worldSpaceCoords = (model2world * vPosition).xyz + vec3(0.5, 0.5, 0.5);
}
</script>
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->

<!-- MAIN FRAGMENT SHADER -->
<script id="fragment-shader" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D backTex; //world coordinates of back faces
uniform sampler2D cubeTex; //voxel_grid
uniform int intensityImage; //1 for intensity image, 0 for rgb
uniform int steps;
uniform float alpha1;
uniform float alpha2;

const int MAX_STEPS = 256;

varying vec4 fPosition;
varying vec3 worldSpaceCoords;

  vec4 sampleAs3DTexture(vec3 texCoord) {
    if (texCoord.z >=  1.0 || texCoord.z <= 0.0)
      return vec4(0.0, 0.0, 0.0, 1.0);
    if (texCoord.x >=  1.0 || texCoord.x <= 0.0)
      return vec4(0.0, 0.0, 0.0, 1.0);
    if (texCoord.y >=  1.0 || texCoord.y <= 0.0)
      return vec4(0.0, 0.0, 0.0, 1.0);

    float v_slices = 16.0; //pass from view to shader
    float h_slices = 16.0;

    float n = v_slices * h_slices -1.0;
 
    float zSliceNumber1 = floor(texCoord.z * n);
    float zSliceNumber2 = min(zSliceNumber1+1.0, n);

    float row1 = floor(zSliceNumber1/h_slices);
    float row2 = floor(zSliceNumber2/h_slices);
    float col1 = zSliceNumber1 - row1*h_slices;
    float col2 = zSliceNumber2 - row2*h_slices;

    float x1 = (col1+texCoord.x)/h_slices;
    float x2 = (col2+texCoord.x)/h_slices;
    float y1 = (row1+1.0-texCoord.y)/v_slices;
    float y2 = (row2+1.0-texCoord.y)/v_slices;

    vec2 texCoordSlice1 = vec2(x1, y1);
    vec2 texCoordSlice2 = vec2(x2, y2);
    
    vec4 colorSlice1 = texture2D( cubeTex, texCoordSlice1 );
    vec4 colorSlice2 = texture2D( cubeTex, texCoordSlice2 );

    float zDifference = mod(texCoord.z * n, 1.0);

    return mix(colorSlice1, colorSlice2, zDifference) ;
  }

void main() {
  vec2 backtexcoords = (fPosition.xy + 1.0) / 2.0;

  vec3 back = texture2D(backTex, backtexcoords).xyz; //world pos of back, in range [0,1]^3
  if (back.x == 0.0 && back.y == 0.0 && back.z ==0.0) { //remove artifacts
    //don't black outhte back corner
    //check all its neighbors. if one of them is black, it must be a boundary pixel, but how much to shift?

    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
    return;
  }
  vec3 front = worldSpaceCoords; //world pos of front, in range [0,1]^3
  
  vec3 dir = back - front;
  float rayLength = length(dir);

  float delta = 1.0/float(steps);
  vec3 r = normalize(dir) * delta;
  vec3 curr = front;
  vec4 accColor = vec4(0.0, 0.0, 0.0, 0.0);
  float accAlpha = 0.0;
  float accLength = 0.0;
  vec4 colorSample;
  float alphaSample;

  //If we have twice as many samples, we only need ~1/2 the alpha per sample.
  //Scaling by 256/10 just happens to give a good value for the alphaCorrection slider.
  float alphaScaleFactor = 256.0 *  delta;

  for (int i=0; i<MAX_STEPS; i++) {
    colorSample =  sampleAs3DTexture(curr);
    alphaSample = colorSample.a;
    colorSample.a = 1.0;

    if (intensityImage == 1)
      colorSample = vec4(1.0, 1.0, 1.0, 1.0);
    else {
      //for 4d space: all colored voxels have equal opacity
      if (alphaSample == 1.0) {
        alphaSample = alpha1;
        colorSample = vec4(1.0, 1.0, 1.0, 1.0);
      } else if (alphaSample > 0.0)
        // alphaSample = 1.0 - alphaSample;
        alphaSample = alpha2;
    }

    // alphaSample *= alphaCorrection;
    alphaSample *= alphaScaleFactor;
    
    accColor += (1.0 - accAlpha) * colorSample * alphaSample;
    accAlpha += alphaSample;

    curr += r;
    accLength += delta;

    if (accLength >=rayLength || accAlpha >= 1.0 || i == (steps+1))
      break;
  }

  if (accAlpha < 1.0)
    accColor  = accAlpha * accColor;
  gl_FragColor = vec4(accColor.xyz, 1.0);
}
</script>


<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->
<!--  -->








<!--  first render pass to get back face coordinates  -->
<script id="texture-vertex-shader" type="x-shader/x-vertex">
attribute vec4 vPosition;

uniform mat4 model2clip;
uniform mat4 model2world;

varying vec3 worldSpaceCoords;

void main()
{
    worldSpaceCoords = (model2world * vPosition).xyz + vec3(0.5, 0.5, 0.5);
    gl_Position = model2clip * vPosition;
}
</script>

<script id="texture-fragment-shader" type="x-shader/x-fragment">
precision mediump float;

varying vec3 worldSpaceCoords;

void main() {
  
  vec3 color = worldSpaceCoords;
  if (color.z == 0.0)
    color.z = 1e-6; //we need to be able to distinguish between background and real back texture

  gl_FragColor  = vec4( color, 1.0 );   
}
</script>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/PVMat.js"></script>
<script type="text/javascript" src="../Common/Texture2D.js"></script>
<script type="text/javascript" src="cube.js"></script>
<script type="text/javascript" src="view.js"></script>
<div>
zoom 1x <input id="slider" type="range"
 min="1" max="20" step="0.5" value="10" />
 10x
</div>
<div>
alpha1 <input id="alpha1" type="range"
 min="0.001" max="0.15" step="0.0005" value="100"/>
</div>
<div>
alpha2 <input id="alpha2" type="range"
 min="0.001" max="0.15" step="0.0005" value="100"/>
</div>
<div>
steps <input id="stepslider" type="range"
 min="10" max="512" step="2" value="100"/>
</div>

</head>

<body>
<button id="ZPlus">z + 0.1</button>
<button id="ZMinus">z - 0.1</button>
<p>
<canvas id="gl-canvas" width="640" height="640" style="background: black;"> 
Oops ... your browser doesn't support the HTML5 canvas element
</canvas><img src="blue-green-purple.png" width="200" onclick="setSource(0)">
</canvas><img       src="rainbow-map.png" width="200" onclick="setSource(1)">
</canvas><img               src="ryb.png" width="200" onclick="setSource(2)">


</body>
</html>